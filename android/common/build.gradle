plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.jetbrains.kotlin.android)
}

task downloadZipFile(type: MyDownload) {
    target = project.file('src/main/cpp/external/SDL-release-2.30.6.zip')
}

class MyDownload extends DefaultTask {
    @OutputFile
    File target

    @TaskAction
    void download() {
        if (!target.exists()) {
            new URL('https://codeload.github.com/libsdl-org/SDL/zip/refs/tags/release-2.30.6')
                .withInputStream { i -> target.withOutputStream{ it << i } }
        }
    }
}

task downloadAndUnzipFile(dependsOn: downloadZipFile, type: Copy) {
    from zipTree(downloadZipFile.target)
    into project.file('src/main/cpp/external')
}

task copySDLJavaSources(dependsOn: downloadAndUnzipFile, type: Copy) {
    from project.file('src/main/cpp/external/SDL-release-2.30.6/android-project/app/src/main/java/org/libsdl/app')
    into project.file('src/main/java/org/libsdl/app')
}

android {
    namespace 'io.github.webrogue_runtime.common'
    compileSdk 34
    kotlinOptions {
        jvmTarget = '1.8'
    }
    tasks.whenTaskAdded { task ->
        if ((task.name == 'javaPreCompileDebug' || task.name == 'javaPreCompileRelease')) {
            task.dependsOn copySDLJavaSources
        }
        if ((task.name == 'compileDebugKotlin' || task.name == 'compileReleaseKotlin')) {
            task.dependsOn copySDLJavaSources
        }
    }
}

dependencies {
    implementation libs.androidx.core.ktx
    implementation libs.androidx.lifecycle.runtime.ktx
    implementation libs.androidx.activity.compose
    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.ui
    implementation libs.androidx.ui.graphics
    implementation libs.androidx.ui.tooling.preview
    implementation libs.androidx.material3
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
    androidTestImplementation platform(libs.androidx.compose.bom)
    androidTestImplementation libs.androidx.ui.test.junit4
    debugImplementation libs.androidx.ui.tooling
    debugImplementation libs.androidx.ui.test.manifest
}